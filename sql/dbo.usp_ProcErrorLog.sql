
CREATE PROCEDURE 	[Report].[usp_ProcErrorLog]
					@ErrorLogID [int] = 0 OUTPUT		-- contains the ErrorLogID of the row inserted
					, @DatabaseName VARCHAR(100)
					, @Schema VARCHAR(25)
					--, @StoredProc VARCHAR(200)
AS
/* 
======================================================================================================================================
OBJECT NAME: Report.usp_ProcErrorLog
PURPOSE    : To use within CATCH block of stored procedures for error handling on SQL Server.
ACCEPTS    : ErrorLogID
RETURNS    : ErrorLogID
CALLED BY  : stored procedures on sql server
AUTHOR     : Fausto Gonzalez
CREATED    : 12/23/2014
======================================================================================================================================
EXAMPLE CALLS:
--------------------------------------------------------------------------------------------------------------------------------------
EXEC dbo.usp_ProcErrorLog

======================================================================================================================================	
CHANGE LOG:
--------------------------------------------------------------------------------------------------------------------------------------
Developer			    Date			  Purpose		
----------------------  ----------------- --------------------------------------------------------------------------------------------
Michael Mooney	    	12/23/2014		  Stored procedure created

======================================================================================================================================
*/
BEGIN
   SET NOCOUNT ON;

   DECLARE @ProcedureName NVARCHAR(400);
   -- Output parameter value of 0 indicates that error
   -- information was not logged
   SET @ErrorLogID = 0;
   
   BEGIN TRY
      -- Return if there is no error information to log
      
      IF ERROR_NUMBER() IS NULL
          RETURN;
      
      -- Return if inside an uncommittable transaction.
      -- Data insertion/modification is not allowed when
      -- a transaction is in an uncommittable state.
      IF XACT_STATE() = -1
      BEGIN
          PRINT 
          'Cannot log error since the current transaction is in an uncommittable state. '
          + 
          'Rollback the transaction before executing usp_ProcErrorLog in order to successfully log error information.';
          RETURN;
      END
      

      INSERT INTO [Report].[ProcErrorLog]
        (
          -- ErrorTime is autogenerated by table
          [UserName]
         ,[ErrorNumber]
         ,[ErrorSeverity]
         ,[ErrorState]
         ,[ErrorLine]
         ,[ErrorMessage]
         ,[DbName]
         ,[DbSchema]
         ,[ErrorProcedure]

        )
      VALUES
        (
          CONVERT(SYSNAME ,CURRENT_USER)
         ,ERROR_NUMBER()
         ,ERROR_SEVERITY()
         ,ERROR_STATE()
         ,ERROR_LINE()
         ,ERROR_MESSAGE()
         ,@DatabaseName
         ,@Schema
         ,ERROR_PROCEDURE()

        );
      
      -- Pass back the ErrorLogID of the row inserted
      SET @ErrorLogID = SCOPE_IDENTITY();
   END TRY
   BEGIN CATCH
      PRINT 'An error occurred in stored procedure usp_ProcErrorLog: ';
      RETURN -1;
   END CATCH
END;


GO
